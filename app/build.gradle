apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-allopen"
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.spacebitlabs.plantly.OpenClass'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.spacebitlabs.plantly"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 11
        versionName "1.0-alpha4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        resValue 'string', 'images_file_path', "Android/data/" + applicationId + ".debug" + "/files/Pictures"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            Properties keyProps = new Properties()
            File keyProperties = file('../keystore.properties')
            if (keyProperties.exists()) {
                keyProps.load(new FileInputStream(keyProperties))

                storeFile file(keyProps['store'])
                keyAlias keyProps['alias']
                storePassword keyProps['storePass']
                keyPassword keyProps['pass']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
//            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    testOptions.unitTests.includeAndroidResources = true
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // support libs
    implementation "androidx.appcompat:appcompat:$versions.androidx"
    implementation "com.google.android.material:material:$versions.material"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation "androidx.vectordrawable:vectordrawable:1.0.1"
    implementation 'androidx.cardview:cardview:1.0.0'

    // architecture libs
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.androidxArch"
    kapt "androidx.lifecycle:lifecycle-common-java8:$versions.androidxArch"
    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation"
    implementation "androidx.work:work-runtime-ktx:$versions.work"

    // data libs
    implementation 'androidx.room:room-runtime:2.1.0'
    implementation 'androidx.room:room-ktx:2.1.0'
    implementation 'androidx.room:room-rxjava2:2.1.0'
    kapt 'androidx.room:room-compiler:2.1.0'
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.0"
    implementation "com.github.kizitonwose.time:time:1.0.2"
    implementation "com.github.kizitonwose.time:time-android:1.0.2"
    implementation project(path: ':amountformats-android')

    // UI libs
    implementation "com.makeramen:roundedimageview:2.3.0"
    implementation "com.cesarferreira.colorize:colorize:0.2.2"
    implementation "de.hdodenhof:circleimageview:3.0.0"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "androidx.recyclerview:recyclerview-selection:1.0.0"
    implementation project(":photopicker")
    implementation "androidx.preference:preference-ktx:1.1.0-rc01"

    // debug libs
    implementation "com.jakewharton.timber:timber:4.7.1"
    debugImplementation "com.amitshekhar.android:debug-db:1.0.6"

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.0@aar') {
        transitive = true
    }

    // test libs
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "junit:junit:4.12"
    testImplementation "androidx.test:core:1.2.0"
	testImplementation 'androidx.arch.core:core-testing:2.0.1'
    testImplementation 'androidx.room:room-testing:2.1.0'
    testImplementation "androidx.test.ext:truth:1.2.0"
    testImplementation "androidx.test.espresso:espresso-intents:3.2.0"
    testImplementation "androidx.test.espresso:espresso-core:3.2.0"
    testImplementation "org.robolectric:robolectric:4.2.1"

    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test:runner:1.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test.ext:truth:1.2.0"
}
