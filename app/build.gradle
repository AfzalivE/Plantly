apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.spacebitlabs.plantly"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 5
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        resValue 'string', 'images_file_path', "Android/data/" + applicationId + ".debug" + "/files/Pictures"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            Properties keyProps = new Properties()
            File keyProperties = file('../keystore.properties')
            if (keyProperties.exists()) {
                keyProps.load(new FileInputStream(keyProperties))

                storeFile file(keyProps['store'])
                keyAlias keyProps['alias']
                storePassword keyProps['storePass']
                keyPassword keyProps['pass']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

ext {
    support_version = "28.0.0"
    nav_version = "1.0.0-alpha09"
    work_version = "1.0.0-beta02"
    room_version = "1.1.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.0'

    // support libs
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support.constraint:constraint-layout:2.0.0-alpha3"
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"

    // architecture libs
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"
    implementation "android.arch.work:work-runtime-ktx:$work_version"

    // data libs
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"
    implementation "com.jakewharton.threetenabp:threetenabp:1.1.0"
    implementation "com.github.kizitonwose.time:time:1.0.2"
    implementation "com.github.kizitonwose.time:time-android:1.0.2"

    // UI libs
    implementation "com.makeramen:roundedimageview:2.3.0"
    implementation "com.cesarferreira.colorize:colorize:0.2.2"
    implementation "de.hdodenhof:circleimageview:2.2.0"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation project(":photopicker")

    // debug libs
    implementation "com.jakewharton.timber:timber:4.7.1"
    debugImplementation "com.amitshekhar.android:debug-db:1.0.4"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }

    // test libs
    testImplementation "junit:junit:4.12"
	testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation "android.arch.persistence.room:testing:1.1.1"

    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}
